version = '0.10'

apply plugin: 'java'
apply plugin: 'application'

repositories {
	jcenter()
}

dependencies {
	compile 'org.fxmisc.richtext:richtextfx:0.9.0'
	compile 'com.miglayout:miglayout-javafx:5.1'
	compile 'de.jensd:fontawesomefx-fontawesome:4.7.0-9'
	compile 'org.controlsfx:controlsfx:9.0.0'
	compile 'org.fxmisc.cssfx:cssfx:1.0.0'
	compile 'org.apache.commons:commons-lang3:3.7'

	def flexmarkVersion = '0.32.20'
	compile 'com.vladsch.flexmark:flexmark:' + flexmarkVersion
	compile 'com.vladsch.flexmark:flexmark-ext-abbreviation:' + flexmarkVersion
	compile 'com.vladsch.flexmark:flexmark-ext-anchorlink:' + flexmarkVersion
	compile 'com.vladsch.flexmark:flexmark-ext-aside:' + flexmarkVersion
	compile 'com.vladsch.flexmark:flexmark-ext-autolink:' + flexmarkVersion
	compile 'com.vladsch.flexmark:flexmark-ext-definition:' + flexmarkVersion
	compile 'com.vladsch.flexmark:flexmark-ext-footnotes:' + flexmarkVersion
	compile 'com.vladsch.flexmark:flexmark-ext-gfm-strikethrough:' + flexmarkVersion
	compile 'com.vladsch.flexmark:flexmark-ext-gfm-tables:' + flexmarkVersion
	compile 'com.vladsch.flexmark:flexmark-ext-gfm-tasklist:' + flexmarkVersion
	compile 'com.vladsch.flexmark:flexmark-ext-toc:' + flexmarkVersion
	compile 'com.vladsch.flexmark:flexmark-ext-wikilink:' + flexmarkVersion
	compile 'com.vladsch.flexmark:flexmark-ext-yaml-front-matter:' + flexmarkVersion

	def commonmarkVersion = '0.11.0'
	compile 'com.atlassian.commonmark:commonmark:' + commonmarkVersion
	compile 'com.atlassian.commonmark:commonmark-ext-autolink:' + commonmarkVersion
	compile 'com.atlassian.commonmark:commonmark-ext-gfm-strikethrough:' + commonmarkVersion
	compile 'com.atlassian.commonmark:commonmark-ext-gfm-tables:' + commonmarkVersion
	compile 'com.atlassian.commonmark:commonmark-ext-heading-anchor:' + commonmarkVersion
	compile 'com.atlassian.commonmark:commonmark-ext-ins:' + commonmarkVersion
	compile 'com.atlassian.commonmark:commonmark-ext-yaml-front-matter:' + commonmarkVersion

	testCompile 'junit:junit:4.12'
}

sourceCompatibility = 9
targetCompatibility = 9

mainClassName = 'org.markdownwriterfx.MarkdownWriterFXApp'

jar {
	manifest {
		attributes( 'Main-Class': 'org.markdownwriterfx.MarkdownWriterFXApp',
					'Class-Path': configurations.compile.collect { it.getName() }.join(' '),
					'Implementation-Version': version
		)
	}
}

distributions {
	main {
		contents {
			from { ['LICENSE', 'README.md', 'CHANGES.md'] }
			into( 'images' ) {
				from { 'images' }
			}
		}
	}
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.7'
}

//---- eclipse ----------------------------------------------------------------

apply plugin: 'eclipse'

import org.gradle.plugins.ide.eclipse.model.AccessRule

eclipse {
	classpath {
		file {
			whenMerged {
				def jre = entries.find { it.path.contains 'org.eclipse.jdt.launching.JRE_CONTAINER' }

				// make JavaFX API accessible in Eclipse project
				// (when refreshing Gradle project in buildship)
				jre.accessRules.add(new AccessRule('0', 'javafx/**'))
				jre.accessRules.add(new AccessRule('0', 'com/sun/javafx/**'))

				// remove trailing slash from jre path
				if (jre.path.endsWith('/'))
					jre.path = jre.path.substring(0, jre.path.length() - 1)
			}
		}
	}
}
